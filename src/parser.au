
// =============================== //
//         Imports & Types         //
// =============================== //

import auro.system {
  void println (string);
  void exit (int);
  void error (string) as syserr;
}

import aulang.util { string readall (string); }

import auro.string {
  string itos (int);
}

import auro.array (NodeShell) {
  type `` as NodeArr {
    NodeShell get (int);
    void push (NodeShell);
    int len ();
  }
  NodeArr empty () as EmptyNodeArr;
}

type NodeShell (Node);

import aulang.node {
  type Node {
    new (string tp, string val);
    string tp;
    string val;
    int line;
    Node child (int);
    void push (Node);
    int len ();

    void print (string indentation);
    Node inline (int);
  }
}

import aulang.lexer {
  TkArr tokens (string);

  type token {
    string tp;
    string val;
    int line;
  }
}

import auro.array(token) {
  type `` as TkArr {
    token get (int);
    int len ();
  }
}

struct Parser {
  TkArr tokens;
  int pos;

  token peek (Parser this) {
    return this.tokens[this.pos];
  }

  token peekat(Parser this, int p) {
    return this.tokens[this.pos + p];
  }

  token next (Parser this) {
    token tk = this.peek();
    this.pos = this.pos + 1;
    return tk;
  }

  string getname (Parser this) {
    token tk = this.next();
    check(tk, "name");
    return tk.val;
  }

  bool maybe (Parser this, string tp) {
    if (this.peek().tp == tp) {
      this.next();
      return true;
    }
    return false;
  }

  int line (Parser this) {
    return this.peek().line;
  }
}

void error (string msg, token tk) {
  string pos = "line " + itos(tk.line);
  if (tk.tp == "eof") pos = "end of file";
  println("Parse error: " + msg + ", at " + pos);
  exit(1);
}

void check (token tk, string tp) {
  if (!(tk.tp == tp)) {
    error("expected " + tp + " but got " + tk.tp, tk);
  }
}




// =============================== //
//            Expressions          //
// =============================== //


Node parseType (Parser p) {
  int line = p.line();
  Node node = new Node("type", p.getname()).inline(line);
  repeat:
    if (p.maybe("?")) {
      Node basenode = node;
      node = new Node("null", "").inline(line);
      node.push(basenode);
      goto repeat;
    }
    if (!(p.peek().tp == "[")) goto end;
    if (!(p.peekat(1).tp == "]")) goto end;
    p.next(); p.next();
    Node basenode = node;
    node = new Node("array", "").inline(line);
    node.push(basenode);
    goto repeat;
  end:
  return node;
}


bool isUnop (string ty) {
  return (ty == "-") || (ty == "!");
}

bool isBinop (string ty) {
  if (ty == "+") return true;
  if (ty == "-") return true;
  if (ty == "*") return true;
  if (ty == "/") return true;
  if (ty == "<") return true;
  if (ty == ">") return true;
  if (ty == "<=") return true;
  if (ty == "==") return true;
  if (ty == ">=") return true;
  if (ty == "!=") return true;
  if (ty == "&&") return true;
  if (ty == "||") return true;
  return false;
}

Node parseBaseExpr (Parser p) {
  int line = p.line();
  Node node;
  token tk = p.next();
  string ty = tk.tp;
  if (ty == "(") {
    Node node = parseExpr(p);
    check(p.next(), ")");
    return node;
  } else if (ty == "num") {
    node = new Node("num", tk.val);
  } else if (ty == "str") {
    node = new Node("str", tk.val);
  } else if (ty == "true") {
    node = new Node("true", "");
  } else if (ty == "false") {
    node = new Node("false", "");
  } else if (ty == "name") {
    node = new Node("var", tk.val);
  } else if (ty == "new") {
    node = new Node("new", "");
    node.push(parseType(p));
    check(p.next(), "(");
    node.push(parseExprList(p, ")"));
  } else { error("invalid expression", tk); }
  return node.inline(line);
}

Node parseSuffix (Parser p) {
  if (isUnop(p.peek().tp)) {
    int line = p.line();
    string op = p.next().tp;
    Node base = parseSuffix(p);
    Node node = new Node("unop", op);
    node.push(base);
    return node.inline(line);
  }
  Node base = parseBaseExpr(p);
  suffix:
    int line = p.line();
    if (p.maybe("(")) {
      Node nxt = new Node("call", "");
      nxt.push(base);
      nxt.push(parseExprList(p, ")"));
      base = nxt.inline(line);
      goto suffix;
    } else if (p.maybe(".")) {
      Node nxt = new Node("field", p.getname());
      nxt.push(base);
      base = nxt.inline(line);
      goto suffix;
    } else if (p.maybe("[")) {
      Node nxt = new Node("index", "");
      nxt.push(base);
      nxt.push(parseExpr(p));
      check(p.next(), "]");
      base = nxt.inline(line);
      goto suffix;
    }
  int line = p.line();
  while (p.maybe("as")) {
    Node nxt = new Node("cast", "");
    if (p.maybe("?")) nxt.tp = "anycast";
    nxt.push(base);
    nxt.push(parseType(p));
    base = nxt.inline(line);
  }
  if (p.maybe("is")) {
    Node nxt = new Node("is", "");
    nxt.push(base);
    nxt.push(parseType(p));
    base = nxt.inline(line);
  }
  return base;
}

Node parseExpr (Parser p) {
  Node left = parseSuffix(p);
  while (isBinop(p.peek().tp)) {
    string op = p.next().tp;
    Node right = parseSuffix(p);
    Node node = new Node("binop", op);
    if (op == "||") node.tp = "logic";
    if (op == "&&") node.tp = "logic";
    node.push(left);
    node.push(right);
    left = node;
  }
  return left;
}

Node parseExprList (Parser p, string end) {
  Node node = new Node("exprlist", "");
  if (p.peek().tp == end) goto end;
  nextexpr:
    node.push(parseExpr(p));
    if (p.maybe(",")) goto nextexpr;
  end:
  check(p.next(), end);
  return node;
}




// =============================== //
//            Statements           //
// =============================== //

string parseLongName (Parser p) {
  string name = "";
  nextname:
    name = name + p.getname();
    if (p.maybe(".")) {
      name = name + "\x1f";
      goto nextname;
    }
    if (p.maybe(":")) {
      name = name + "\x1d";
      goto nextname;
    }
    return name;
}

Node parseNameList (Parser p, string tp, string sep) {
  Node node = new Node(tp, "");
  repeat:
    node.push(new Node("name", p.getname()));
    if (p.peek().tp == sep) {
      p.next();
      goto repeat;
    }
  return node;
}

// Items that start with an identifier
Node parseIdentItem (Parser p) {
  token _tk = p.peek();
  int line = p.line();
  Node outs = new Node("outs", "");
  if (!p.maybe("void")) {
    nextout:
    outs.push(parseTopLevelItem(p));
    if (p.maybe(",")) goto nextout;
  }

  string name = parseLongName(p);

  if (p.maybe("(")) {
    Node ins = new Node("ins", "");
    if (p.peek().tp == ")") goto endin;
    nextin:
      Node argNode = new Node("arg", "");

      argNode.push(parseTopLevelItem(p));

      if (p.peek().tp == "name")
        argNode.val = p.getname();
      ins.push(argNode);
      if (p.peek().tp == ",") {
        p.next();
        goto nextin;
      }
    endin:
    check(p.next(), ")");

    string alias = "";
    if (p.maybe("as")) alias = p.getname();

    Node node = new Node("function", name).inline(line);
    node.push(ins);
    node.push(outs);
    node.push(new Node("alias", alias));
    return node;
  } else if (outs.len() == 1) {
    if (p.maybe(";")) {
      Node tpnode = outs.child(0);
      Node declnode = new Node("decl", name);
      declnode.line = line;
      declnode.push(tpnode);
      return declnode;
    }
    if (p.maybe("as")) {
      Node node = new Node("alias", p.getname());
      node.push(new Node("item", name));
      return node;
    }
    if (p.maybe("=")) {
      Node tpnode = outs.child(0);
      Node node = new Node("decl_assign", name);
      node.line = line;
      node.push(tpnode);
      node.push(parseExpr(p));
      check(p.next(), ";");
      return node;
    }
  }
  check(p.peek(), "("); // already failed '(', but give error message
}


Node parseImport (Parser p) {
  // import keyword already consumed

  Node result = new Node("import", "");
  if (p.maybe("module")) result.tp = "import-module";
  result.val = parseLongName(p);

  bool hasbody = false;

  Node argnode = new Node("none", "");
  if (p.maybe("(")) {
    hasbody = true;
    if (p.maybe("module")) {
      argnode.tp = "module";
      argnode.val = p.getname();
      goto endarg;
    }
    argnode.tp = "arglist";
    if (p.peek().tp == ")") goto endarg;
    nextarg:
      Node argNode = new Node("name", p.getname());
      if (p.maybe("as")) {
        Node prev = argNode;
        argNode = new Node("alias", p.getname());
        argNode.push(prev);
      }
      argnode.push(argNode);
      if (p.maybe(",")) goto nextarg;
    endarg:
    check(p.next(), ")");
  }

  if (p.peek().tp == "{") hasbody = true;

  if (hasbody) {
    check(p.next(), "{");

    Node bodynode = new Node("body", "");
    bodynode.push(argnode);

    nextitem:
      token _tk = p.peek();
      int line = p.line();
      if (p.maybe("}")) goto end;
      else if (p.maybe("type")) {
        Node typenode = new Node("type", parseLongName(p));

        string alias = "";
        if (p.maybe("as")) alias = p.getname();
        typenode.push(new Node("alias", alias));

        if (p.maybe("{")) {
          nextmember:
          int line = p.line();
          if (p.maybe("new")) {
            Node item = new Node("new", "").inline(line);
            Node ins = new Node("ins", "");
            check(p.next(), "(");
            if (p.peek().tp == ")") goto endin;
            nextin:
              Node inNode = new Node("arg", "");
              inNode.push(new Node("type", p.getname()));
              string argname = "";
              if (p.peek().tp == "name")
                argname = p.getname();
              inNode.push(new Node("name", argname));
              ins.push(inNode);
              if (p.peek().tp == ",") {
                p.next();
                goto nextin;
              }
            endin:
            check(p.next(), ")");
            check(p.next(), ";");
            item.push(ins);
            typenode.push(item);
          } else {
            Node item = parseIdentItem(p);
            if (item.tp == "function")
              check(p.next(), ";");
            typenode.push(item);
          }
          if (!p.maybe("}")) goto nextmember;
        } else check(p.next(), ";");

        bodynode.push(typenode.inline(line));
      } else if (p.maybe("module")) {
        Node item = new Node("module", parseLongName(p));
        string alias = "";
        if (p.maybe("as")) alias = p.getname();
        item.push(new Node("alias", alias));
        check(p.next(), ";");
        bodynode.push(item.inline(line));
      } else {
        Node item = parseIdentItem(p);
        if (item.tp == "function")
          check(p.next(), ";");
        else error("???", _tk);
        bodynode.push(item);
      }
      goto nextitem;
    end:

    result.push(bodynode);
  }

  return result;
}

Node parseAssignment (Parser p, Node first) {
  Node left = new Node("exprlist", "");
  left.push(first);

  nextleft:
    if (!p.maybe(",")) goto endleft;
    left.push(parseExpr(p));
    goto nextleft;
  endleft:
  check(p.next(), "=");

  Node expr = parseExpr(p);
  check(p.next(), ";");

  Node node = new Node("assignment", "");
  node.push(left);
  node.push(expr);
  return node;
}

Node parseDecl (Parser p) {
  Node node = new Node("decl", "");
  node.push(parseType(p));

  nextpart:
    Node partnode = new Node("declpart", p.getname());
    if (p.maybe("=")) partnode.push(parseExpr(p));
    node.push(partnode);
    if (p.maybe(",")) goto nextpart;
  check(p.next(), ";");

  return node;
}

Node parseStmt (Parser p) {
  int line = p.line();
  token _tk = p.peek();
  if (p.peek().tp == "{") return parseBlock(p);
  if (p.maybe("goto")) {
    Node node = new Node("goto", p.getname());
    check(p.next(), ";");
    return node.inline(line);
  }
  if (p.maybe("return")) {
    Node node = new Node("return", "");
    node.push(parseExprList(p, ";"));
    return node.inline(line);
  }
  if (p.maybe("break")) {
    Node node = new Node("break", "");
    if (p.peek().tp == "name")
      node.val = p.getname();
    check(p.next(), ";");
    return node.inline(line);
  }
  if (p.maybe("if")) {
    Node node = new Node("if", "");
    check(p.next(), "(");
    node.push(parseExpr(p));
    check(p.next(), ")");
    node.push(parseStmt(p));
    if (p.maybe("else"))
      node.push(parseStmt(p));
    return node.inline(line);
  }
  if (p.maybe("while")) {
    Node node = new Node("while", "");
    check(p.next(), "(");
    node.push(parseExpr(p));
    check(p.next(), ")");
    node.push(parseStmt(p));
    return node.inline(line);
  }
  if (p.peek().tp == "name") {
    string ty = p.peekat(1).tp;
    if (ty == ":") {
      string name = p.getname(); p.next();
      if (p.peek().tp == "while") {
        Node loop = parseStmt(p);
        loop.val = name;
        return loop;
      } else {
        return new Node("label", name).inline(line);
      }
    }
    if (ty == "[")
      if (p.peekat(2).tp == "]")
        return parseDecl(p).inline(line);
    if (ty == "?") return parseDecl(p).inline(line);
    if (ty == "name") return parseDecl(p).inline(line);
  }
  Node expr = parseExpr(p);
  if (p.peek().tp == "=") return parseAssignment(p, expr).inline(line);
  if (p.peek().tp == ",") return parseAssignment(p, expr).inline(line);
  if (expr.tp == "call") {
    check(p.next(), ";");
    return expr;
  }
  error("invalid statement", _tk);
}

Node parseBlock (Parser p) {
  Node result = new Node("block", "");
  check(p.next(), "{");
  repeat:
  if (p.maybe("}")) return result;
  int line = p.line();
  Node stmt = parseStmt(p).inline(line);
  result.push(stmt);
  goto repeat;
}

Node parseTopLevelItem (Parser p) {
  int line = p.line();

  if (p.maybe("$."))
    return new Node("generic_arg", p.getname()).inline(line);

  Node node = new Node("item", p.getname());
  while (p.maybe(".")) {
    Node _node = new Node("field", p.getname());
    _node.push(node);
    node = _node;
  }

  if (p.maybe("$<")) {
    Node _generic = new Node("generic", "");
    _generic.push(node.inline(line));
    node = _generic;

    loop:
      Node decl = new Node("decl", p.getname());
      check(p.next(), "=");
      Node item = parseTopLevelItem(p);
      decl.push(item);
      node.push(decl);
      if (p.maybe(",")) goto loop;
    check(p.next(), ">");
  }

  return node.inline(line);
}

Node parseModule (Parser p) {
  Node modnode = new Node("module-def", p.getname());

  token generic_tk = p.peek();
  bool generic = false;
  if (p.maybe("$<")) {
    generic = true;
    check(p.next(), ">");
  }

  if (p.maybe("=")) {
    modnode.tp = "module-assign";
    Node valnode;
    if (p.maybe("import")) {
      valnode = new Node("import", parseLongName(p));
    } else {
      valnode = new Node("module", p.getname());
      if (p.maybe("(")) {
        valnode.tp = "functor";
        check(p.next(), "module");
        valnode.push(new Node("module", p.getname()));
        check(p.next(), ")");
      }
    }
    check(p.next(), ";");
    modnode.push(valnode);
    if (generic) modnode.push(new Node("generic", ""));
    return modnode;
  }

  check(p.next(), "{");
  nextitem:
    if (p.peek().tp == "}") goto enditem;
    Node itemnode = new Node("item", p.getname());
    check(p.next(), "=");
    itemnode.push(parseTopLevelItem(p));
    check(p.next(), ";");
    modnode.push(itemnode);
    goto nextitem;
  enditem:
  check(p.next(), "}");

  if (generic) error("definition modules can't be generic", generic_tk);

  return modnode;
}

Node parseTopLevel (Parser p) {
  token _tk = p.peek();
  if (p.maybe("import")) {
    //return parseImport(p).inline(_tk.line);
    Node node = new Node("import", parseLongName(p)).inline(_tk.line);
    check(p.next(), ";");
    return node;
  }
  if (p.maybe("extern")) {
    error("extern statements not yet supported", _tk);
  }
  if (p.maybe("export")) {
    Node node = new Node("export", "");
    node.push(parseTopLevelItem(p));
    if (p.maybe("as")) {
      node.tp = "export-alias";
      node.val = p.getname();
    }
    check(p.next(), ";");
    return node.inline(_tk.line);
  }

  bool ispriv = p.maybe("private");

  Node node;
  if (p.maybe("struct")) {
    Node typenode = new Node("struct", parseLongName(p));
    check(p.next(), "{");
    nextmember:
      if (p.maybe("}")) goto endmember;
      Node item = parseIdentItem(p);
      if (item.tp == "function")
        item.push(parseBlock(p));
      typenode.push(item);
      goto nextmember;
    endmember:
    node = typenode.inline(_tk.line);
  } else if (p.maybe("type")) {
    Node typenode = new Node("type", parseLongName(p));
    if (p.maybe("(")) {
      check(p.next(), "(");
      Node base = parseType(p);
      typenode.push(base);
      check(p.next(), ")");
      check(p.next(), ";");
    } else if (p.maybe("=")) {
      typenode.tp = "type-assign";
      typenode.push(parseTopLevelItem(p));
      check(p.next(), ";");
    }
    node = typenode.inline(_tk.line);
  } else if (p.maybe("module")) {
    node = parseModule(p).inline(_tk.line);
  } else {
    node = parseIdentItem(p);
    if (node.tp == "function") {
      if (p.maybe("=")) {
        node.push(parseTopLevelItem(p));
        check(p.next(), ";");
      } else {
        node.push(parseBlock(p));
      }
    } else if (!(node.tp == "decl_assign")) {
      error("invalid top level statement", _tk);
    }
  }
  //if (p.maybe("module")) return parseModule(p);

  if (ispriv) {
    Node privnode = new Node("private", "");
    privnode.push(node);
    return privnode;
  } else return node;
}




// =============================== //
//             Interface           //
// =============================== //

Node parse (string src) {
  Node result = new Node("program", "");

  Parser parser = new Parser(tokens(src), 0);

  repeat:
    if (parser.peek().tp == "eof") goto end;
    Node node = parseTopLevel(parser);
    result.push(node);
    goto repeat;
  end:

  return result;
}

void main () {
  string src = readall("test.au");
  Node program = parse(src);
  program.print("");
}